\begin{comment}
\documentclass[10pt]{article}
\usepackage{fullpage, graphicx, url}
\setlength{\parskip}{1ex}
\setlength{\parindent}{0ex}
\title{\$NAME}
\begin{document}


\begin{tabular}{ccc}
The Alternative Csound Reference Manual & & \\
Previous & &Next

\end{tabular}

%\hline 
\end{comment}
\section{\$NAME}
\$NAME --  Calls a defined macro. \subsection*{Description}


  Macros are textual replacements which are made in the orchestra as it is being read. The macro system in Csound is a very simple one, and uses the characters \# and \$ to define and call macros. This can save typing, and can lead to a coherent structure and consistent style. This is similar to, but independent of, the \emph{macro system in the score language}
. 


 \emph{\$NAME}
 -- calls a defined macro. To use a macro, the name is used following a \$ character. The name is terminated by the first character which is neither a letter nor a number. If it is necessary for the name not to terminate with a space, a period, which will be ignored, can be used to terminate the name. The string, \emph{\$NAME}
., is replaced by the replacement text from the definition. The replacement text can also include macro calls. 
\subsection*{Syntax}


 \textbf{\$NAME}

\subsection*{Initialization}


 \emph{\# replacement text \#}
 -- The replacement text is any character string (not containing a \#) and can extend over mutliple lines. The replacement text is enclosed within the \# characters, which ensure that additional characters are not inadvertently captured. 
\subsection*{Performance}


  Some care is needed with textual replacement macros, as they can sometimes do strange things. They take no notice of any meaning, so spaces are significant. This is why, unlike the C programming language, the definition has the replacement text surrounded by \# characters. Used carefully, this simple macro system is a powerful concept, but it can be abused. 
\subsection*{Examples}


  Here is an example of the calling a macro. It uses the files \emph{define.orc}
 and \emph{define.sco}
. 


 \textbf{Example 1. An example of the calling a macro.}

\begin{lstlisting}
/* define.orc */
; Initialize the global variables.
sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

; Define the macros.
#define VOLUME #5000#
#define FREQ #440#
#define TABLE #1#

; Instrument #1
instr 1
  ; Use the macros.
  ; This will be expanded to "a1 oscil 5000, 440, 1".
  a1 oscil $VOLUME, $FREQ, $TABLE

  ; Send it to the output.
  out a1
endin
/* define.orc */
        
\end{lstlisting}
\begin{lstlisting}
/* define.sco */
; Define Table #1 with an ordinary sine wave.
f 1 0 32768 10 1 
         
; Play Instrument #1 for two seconds.
i 1 0 2
e
/* define.sco */
        
\end{lstlisting}
 Its output should include lines like this: \begin{lstlisting}
Macro definition for VOLUME
Macro definition for CPS
Macro definition for TABLE
      
\end{lstlisting}


  Here is an example of the calling a macro with arguments. It uses the files \emph{define\_args.orc}
 and \emph{define\_args.sco}
. 


 \textbf{Example 2. An example of the calling a macro with arguments.}

\begin{lstlisting}
/* define_args.orc */
; Initialize the global variables.
sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

; Define the oscillator macro.
#define OSCMACRO(VOLUME'FREQ'TABLE) #oscil $VOLUME, $FREQ, $TABLE#

; Instrument #1
instr 1
  ; Use the oscillator macro.
  ; This will be expanded to "a1 oscil 5000, 440, 1".
  a1 $OSCMACRO(5000'440'1)

  ; Send it to the output.
  out a1
endin
/* define_args.orc */
        
\end{lstlisting}
\begin{lstlisting}
/* define_args.sco */
; Define Table #1 with an ordinary sine wave.
f 1 0 32768 10 1 
         
; Play Instrument #1 for two seconds.
i 1 0 2
e
/* define_args.sco */
        
\end{lstlisting}
 Its output should include a line like this: \begin{lstlisting}
Macro definition for OSCMACRO
      
\end{lstlisting}
\subsection*{See Also}


 \emph{\#define}
, \emph{\#undef}

\subsection*{Credits}


 


 


\begin{tabular}{cccc}
Author: John ffitch &University of Bath/Codemist Ltd. &Bath, UK &April, 1998

\end{tabular}



 


 Example written by Kevin Conder.


 New in Csound version 3.48
%\hline 


\begin{comment}
\begin{tabular}{lcr}
Previous &Home &Next \\
\#undef &Up &\%

\end{tabular}


\end{document}
\end{comment}
