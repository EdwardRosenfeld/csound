\begin{comment}
\documentclass[10pt]{article}
\usepackage{fullpage, graphicx, url}
\setlength{\parskip}{1ex}
\setlength{\parindent}{0ex}
\title{if}
\begin{document}


\begin{tabular}{ccc}
The Alternative Csound Reference Manual & & \\
Previous & &Next

\end{tabular}

%\hline 
\end{comment}
\section{if}
if --  Branches conditionally at initialization or during performance time. \subsection*{Description}


 \emph{if...igoto}
 -- conditional branch at initialization time, depending on the truth value of the logical expression \emph{ia}
 \emph{R}
 \emph{ib}
. The branch is taken only if the result is true. 


 \emph{if...kgoto}
 -- conditional branch during performance time, depending on the truth value of the logical expression \emph{ka}
 \emph{R}
 \emph{kb}
. The branch is taken only if the result is true. 


 \emph{if...goto}
 -- combination of the above. Condition tested on every pass. 


 \emph{if...then}
 -- allows the ability to specify conditional \emph{if}
/\emph{else}
/\emph{endif}
 blocks. All \emph{if...then}
 blocks must end with an \emph{endif}
 statement. \emph{elseif}
 and \emph{else}
 statements are optional. Any number of \emph{elseif}
 statements are allowed. Only one \emph{else}
 statement may occur and it must be the last conditional statement before the \emph{endif}
 statement. Nested \emph{if...then}
 blocks are allowed. 


 


\begin{tabular}{cc}
\textbf{Note}
 \\
  &

  Note that if the condition uses a k-rate variable (for instance, ``if kval $>$ 0''), the \emph{if...goto}
 or \emph{if...then}
 statement will be ignored during the i-time pass. This allows for opcode initialization, even if the k-rate variable has already been assigned an appropriate value by an earlier init statement. 


\end{tabular}

\subsection*{Syntax}


 \textbf{if}
 ia R ib \textbf{igoto}
 label


 \textbf{if}
 ka R kb \textbf{kgoto}
 label


 \textbf{if}
 ia R ib \textbf{goto}
 label


 \textbf{if}
 xa R xb \textbf{then}



  where \emph{label}
 is in the same instrument block and is not an expression, and where \emph{R}
 is one of the Relational operators (\emph{$<$}
, \emph{=}
, \emph{$<$=}
, \emph{==}
, \emph{!=}
) (and \emph{=}
 for convenience, see also under \emph{Conditional Values}
). 
\subsection*{Examples}


  Here is an example of the if...igoto combination. It uses the files \emph{igoto.orc}
 and \emph{igoto.sco}
. 


 \textbf{Example 1. Example of the if...igoto combination.}

\begin{lstlisting}
/* igoto.orc */
; Initialize the global variables.
sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

; Instrument #1.
instr 1
  ; Get the value of the 4th p-field from the score.
  iparam = p4

  ; If iparam is 1 then play the high note.
  ; If not then play the low note.
  if (iparam == 1) igoto highnote
    igoto lownote

highnote:
  ifreq = 880
  goto playit

lownote:
  ifreq = 440
  goto playit

playit:
  ; Print the values of iparam and ifreq.
  print iparam
  print ifreq

  a1 oscil 10000, ifreq, 1
  out a1
endin
/* igoto.orc */
        
\end{lstlisting}
\begin{lstlisting}
/* igoto.sco */
; Table #1: a simple sine wave.
f 1 0 32768 10 1

; p4: 1 = high note, anything else = low note
; Play Instrument #1 for one second, a low note.
i 1 0 1 0
; Play a Instrument #1 for one second, a high note.
i 1 1 1 1
e
/* igoto.sco */
        
\end{lstlisting}
 Its output should include lines like this: \begin{lstlisting}
instr 1:  iparam = 0.000
instr 1:  ifreq = 440.000
instr 1:  iparam = 1.000
instr 1:  ifreq = 880.000
      
\end{lstlisting}


  Here is an example of the if...kgoto combination. It uses the files \emph{kgoto.orc}
 and \emph{kgoto.sco}
. 


 \textbf{Example 2. Example of the if...kgoto combination.}

\begin{lstlisting}
/* kgoto.orc */
; Initialize the global variables.
sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

; Instrument #1.
instr 1
  ; Change kval linearly from 0 to 2 over
  ; the period set by the third p-field.
  kval line 0, p3, 2

  ; If kval is greater than or equal to 1 then play the high note.
  ; If not then play the low note.
  if (kval >= 1) kgoto highnote
    kgoto lownote

highnote:
  kfreq = 880
  goto playit

lownote:
  kfreq = 440
  goto playit

playit:
  ; Print the values of kval and kfreq.
  printks "kval = %f, kfreq = %f\\n", 1, kval, kfreq

  a1 oscil 10000, kfreq, 1
  out a1
endin
/* kgoto.orc */
        
\end{lstlisting}
\begin{lstlisting}
/* kgoto.sco */
; Table #1: a simple sine wave.
f 1 0 32768 10 1

; Play Instrument #1 for two seconds.
i 1 0 2
e
/* kgoto.sco */
        
\end{lstlisting}
 Its output should include lines like this: \begin{lstlisting}
kval = 0.000000, kfreq = 440.000000
kval = 0.999732, kfreq = 440.000000
kval = 1.999639, kfreq = 880.000000
      
\end{lstlisting}
\subsection*{Examples}


  Here is an example of the if...then combo. It uses the files \emph{ifthen.orc}
 and \emph{ifthen.sco}
. 


 \textbf{Example 3. Example of the if...then combo.}

\begin{lstlisting}
/* ifthen.orc */
sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

; Instrument #1.
instr 1
  ; Get the note value from the fourth p-field.
  knote = p4

  ; Does the user want a low note?
  if (knote == 0) then
    kcps = 220
  ; Does the user want a middle note?
  elseif (knote == 1) then
    kcps = 440
  ; Does the user want a high note?
  elseif (knote == 2) then
    kcps = 880
  endif

  ; Create the note.
  kamp init 25000
  ifn = 1
  a1 oscili kamp, kcps, ifn

  out a1
endin
/* ifthen.orc */
        
\end{lstlisting}
\begin{lstlisting}
/* ifthen.sco */
; Table #1, a sine wave.
f 1 0 16384 10 1

; p4: 0=low note, 1=middle note, 2=high note.
; Play Instrument #1 for one second, low note.
i 1 0 1 0
; Play Instrument #1 for one second, middle note.
i 1 1 1 1
; Play Instrument #1 for one second, high note.
i 1 2 1 2
e
/* ifthen.sco */
        
\end{lstlisting}
\subsection*{See Also}


 \emph{elseif}
, \emph{else}
, \emph{endif}
, \emph{goto}
, \emph{igoto}
, \emph{kgoto}
, \emph{tigoto}
, \emph{timout}

\subsection*{Credits}


 Examples written by Kevin Conder.


 Added a note by Jim Aikin.


 February 2004. Added a note by Matt Ingalls.
%\hline 


\begin{comment}
\begin{tabular}{lcr}
Previous &Home &Next \\
iexprand &Up &igauss

\end{tabular}


\end{document}
\end{comment}
